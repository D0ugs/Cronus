//===== Cronus Script ===========================================
//= Mercador de Diamantes Raros
//===== Por: ====================================================
//= Z3R0
//===== Versão Atual: ===========================================
//= 1.5
//===== Descrição: ==============================================
//= [Official Conversion]
//= Exchanges Diamante de 17 Quilates
//===== Comentários Adicionais: =================================
//= 1.0 First / Optimized Version
//= 1.1 Adjusted for Missing ; on Line #271 (#15425)
//= 1.2 Removed Comments per Request (#15426)
//= 1.3 Changed Emotion # to Const Definition (#15427)
//= 1.4 Removed Double Space on Line #148 (#15428)
//= 1.5 Added This Version History (#15429)
//= 1.6 Tradução [Evanio]
//===============================================================

-	script	MercadordeDiamantesRaros	1_M_MERCHANT,{

	set .@npc$, "[Mercador de Diamantes Raros]";

	mes .@npc$;
	mes "Senhoras e Senhores!!!";
	mes "Você já deve ter ouvido boatos, mas nunca viu com os próprios olhos";
	mes "o diamante mais raro do mundo!";
	mes "O ^FF82FF'Diamante de 17 Quilates'^000000 está a venda por um excelente preço!!";
	mes "Se você é interessado por diamantes raros, ouça!";
	next;

	mes .@npc$;
	mes "Você pode dar o diamante como presente para seu amor.";
	mes "Garanto que o seu amor irá aprecisar bastante o ^FF82FF'Diamante de 17 Quilates'^000000";
	mes "e encontrará um lindo tesouro no seu coração.";
	emotion e_loud;
	next;

	mes .@npc$;
	mes "O que? Você não tem um amor?";
	mes "Você também pode ficar para si mesmo.";
	mes "É provável que a aura encantadora do ^FF82FF'Diamante de 17 Quilates'^000000";
	mes "irá trazer um grande amor a sua vida.";
	next;

	mes .@npc$;
	mes "Conhecido como o diamante dos diamantes...";
	mes "É ele, o ^FF82FF'Diamante de 17 Quilates'^000000!";
	next;

	set .@menu$, "^FF82FF'Diamante de 17 Quilates'^000000?";
	if (countitem(6024))
		set .@menu$, .@menu$ + ":Trocar meu Diamante de 17 Quilates por zeny...";

	switch(select(.@menu$)) {
	case 1:
		mes .@npc$;
		mes "Sim!";
		mes "Hehe, o ^FF82FF'Diamante de 17 Quilates'^000000 é ser visto agora";
		mes "como o diamante de mais alta qualidade, 17 quilates.";
		mes "Comparando seu tamanho com uma ervilha, rocha e incolor,";
		mes "desenterrado em Morroc, é indescritível!";
		emotion e_omg;
		next;

		mes .@npc$;
		mes "Alguns afirmam que este é o mesmo diamante";
		mes "que foi visto por Kachua em Comodo!";
		next;

		if (select("Terminar conversa:Quanto custa?") == 1) {
			mes .@npc$;
			mes "Ele é bastante barato!";
			mes "Se ficar interessado, fale comigo a qualquer momento!";
			close;
		}

		mes .@npc$;
		mes "Claro! O mais importante é o preço!";
		mes "Como eu já tinha dito, o ^FF82FF'Diamante de 17 Quilates'^000000 foi";
		mes "avaliado como um diamante de 17 quilates.";
		mes "Não há muitos como ele no mundo.";
		next;

		mes .@npc$;
		mes "O preço são de meros 500 milhões de zeny!";
		mes "Há também uma taxa de serviço de 1,000,000z.";
		mes "Comparado com a qualidade do diamante";
		mes "este é um pequeno preço a pagar.";
		next;

		if (select("^828282É muito caro.^000000:Quero comprá-lo.") == 1) {
			mes .@npc$;
			mes "Dizer que um diamante como o";
			mes "^FF82FF'Diamante de 17 Quilates'^000000 é muito caro, ";
			mes "me deixa sem palavras.";
			close;
		}

		mes .@npc$;
		mes "OOOH! Eu sabia do momento que te vi que";
		mes "você era o legítimo proprietário desse diamante.";
		mes "Estou muito feliz de não estar enganado!";
		mes "Eu acredito que esse diamante irá te servir muito bem.";
		mes "Tudo que preciso de você é que assine o contrato.";
		next;

		while(1) {
			if (select("^B9062FEu vou assinar o contrato.^000000:^828282Esquece, eu mudeu de ideia.^000000") == 2) {
				mes .@npc$;
				mes "EU vejo, isso é muito ruim..";
				close;
			}

			mes .@npc$;
			mes "Qual o seu nome?";
			next;

			mes "[" + strcharinfo(0) + "]";
			mes "Meu nome é " + strcharinfo(0) + ".";
			next;

			mes .@npc$;
			mes "Certo, por favor, assine aqui.";
			next;

			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "Ok, tudo certo.";
				mes "Tudo que resta agora é o pagamento.";
				next;

				if (Zeny >= 501000000) {
					mes .@npc$;
					mes ". . . . . .";
					next;

					mes .@npc$;
					mes "Calculando o preço da taxa..";
					mes "501,000,000 zeny.";
					mes "Quantidade confirmada.";
					emotion e_loud;
					set Zeny, Zeny - 501000000;
					getitem 6024, 1;
					next;

					mes .@npc$;
					mes "Você pode trocar seu diamante para zeny novamente a qualquer momento.";
					mes "No entanto, irá existir uma taxa de serviço de 1,000,000z.";
					mes "Obrigado pela negociação.";
					emotion e_thx;
					close;
				}
				else {
					mes .@npc$;
					mes "Hm?";
					mes "Desculpe, mas você não tem fundos suficiente.";
					mes "Incluindo a taxa de serviço, um total de 501,000,000z é necessário.";
					mes "Por favor, verifique seu dinheiro e tente novamente.";
					emotion e_hmm;
					close;
				}
			}
			else {
				mes .@npc$;
				mes "Está é realmente sua assinatura?";
				mes "Pode assinar novamente para eu confirmar?";
				next;
			}
		}
	case 2:
		mes .@npc$;
		mes "AAH. Você gostaria de trocar seu ^FF82FF'Diamante de 17 Quilates'^000000 por zeny?";
		emotion e_gasp;
		next;
		if (select("Sim:Não") == 2) {
			mes .@npc$;
			mes "Então, o que é que você quer?..";
			mes ". . . . . ....";
			emotion e_dots;
			close;
		}

		mes .@npc$;
		mes "Bem... Depois que eu receber sua assinatura, ";
		mes "Você deve oferecer seu ^FF82FF'Diamante de 17 Quilates'^000000.";
		mes "Também não esqueça que você precisa pagar a taxa de serviço de 1,000,000z!";
		next;

		mes .@npc$;
		mes "Qual seu nome?";
		next;

		mes "[" + strcharinfo(0) + "]";
		mes "Meu nome é " + strcharinfo(0) + ".";
		next;

		mes .@npc$;
		mes "Certo, por favor, assine aqui.";
		next;

		while (1) {
			if (select("Assinar:Não assinar") == 2) {
				mes .@npc$;
				mes "A negociação não pode ser completada por que é necessária sua assinatura.";
				close;
			}

			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "Sua assinatura foi aceita.";
				mes "Agora irei pegar seu ^FF82FF'Diamante de 17 Quilates'^000000..";
				mes "Você receberá ^0000FF499,000,000z^000000.";
				next;

				mes .@npc$;
				mes "Obrigado pela negociação.";
				mes "Sempre que precisar de um ^FF82FF'Diamante de 17 Quilates'^000000,";
				mes "volte a qualquer momento com 500 milhões de zeny.";
				emotion e_thx;
				delitem 6024, 1;
				set Zeny, Zeny + 499000000;
				close;
			}
			else {
				mes .@npc$;
				mes "Esta é realmente a sua assinatura?";
				mes "Assine novamente para que eu possa confirmar.";
				next;
			}
		}
	}
}

prontera,165,89,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#1	1_M_MERCHANT
geffen,106,63,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#2	1_M_MERCHANT
morocc,146,100,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#3	1_M_MERCHANT
alberta,129,60,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#4	1_M_MERCHANT
lighthalzen,163,65,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#5	1_M_MERCHANT
rachel,106,142,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#6	1_M_MERCHANT
payon,180,130,6	duplicate(MercadordeDiamantesRaros)	Mercador de Diamantes#7	1_M_MERCHANT