//===== Cronus Script ===========================================
//= Sistema de Remoção de Cartas
//===== Por: ====================================================
//= Muad_Dib
//===== Versão Atual: ===========================================
//= 1.2
//===== Descrição: ==============================================
//= [Conversão Oficial]
//= Separa cartas de equipamentos.
//===== Comentários Adicionais: =================================
//= 1.0 First Version. [Euphy]
//= 1.1 Added "Richard" NPC. [Euphy]
//= 1.2 Tradução [Evanio]
//===============================================================

-	script	::CardSeparation_mal	-1,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você tem muitos tipos de itens. Tente reduzir essa quantidade para que possamos continuar.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Não é possível continuar porque você tem muitos itens pesados. Reduzir o peso para poder continuar.";
		close;
	}
	if (strnpcinfo(1) == "Jeremy") {
		set .@Jeremy,1;
		set .@n$, "[Jeremy]";
		setarray .@equip_name$[0],  "Armadura",  "Calçados",  "Capa", "Cabeça";
		setarray .@equip_slot[0], EQI_ARMOR,EQI_SHOES,EQI_GARMENT,EQI_HEAD_TOP;

		mes .@n$;
		mes "Quanto tempo~";
		mes "Aprendi uma nova habilidade que separa as cartas de Armaduras, Calçados, Capas e Equipamentos para Cabeça. Você quer tentar?";
	} else {
		set .@Jeremy,0;
		set .@n$, "[Richard]";
		setarray .@equip_name$[0], "Mão esquerda", "Mão direita";
		setarray .@equip_slot[0],   EQI_HAND_L,   EQI_HAND_R;

		mes .@n$;
		mes "O Jeremy não quer nem saber de tocar em armas e escudos porque ainda não consegue remover as cartas de ambos com eficiência. É por isso que me especializei em uma habilidade para separar cartas de armas e escudos ...";
	}
	next;
	mes .@n$;
	mes "Geralmente a taxa é de 1.000.000 Zeny. Durante a separação da carta, você pode usar ^990000itens especiais que reduzem a taxa de destruição de equipamento ou cartas^000000. Não cobramos zeny adicional para isso.";
	next;
	mes .@n$;
	mes "Existe a possibilidade de destruí-los, mesmo usando um item especial. Também, ^ff0000o grau de refino pode ser perdido^000000. Você aceita todos os requesitos para iniciar a separação?";
	next;

	for(set .@i,0; .@i<getarraysize(.@equip_slot); set .@i,.@i+1) {
		if (getequipisequiped(.@equip_slot[.@i]))
			set .@menu$, .@menu$+getequipname(.@equip_slot[.@i])+":";
		else
			set .@menu$, .@menu$+"^999999"+.@equip_name$[.@i]+" (vazio)^000000:";
	}

	set .@i, select("Pare:"+((.@Jeremy)?"Como é possível?":"")+":"+.@menu$);
	switch(.@i) {
	case 1:
		mes .@n$;
		mes "Sempre que você precisar dos meus serviços, visite-me aqui.";
		close;
	case 2:
		mes .@n$;
		mes "Você quer saber o que é tão especial. Bem, eu odeio dar apenas um guia, então deixe-me contar a história ...";
		next;
		mes "^000099Jeremy está alongando os ombros e mãos. Ele pode estar à espera de alguém para conversar com ele.^000000";
		next;
		mes .@n$;
		mes "Você sabe que a especialidade de Malangdo é comida enlatada?";
		next;
		select("Eu sei muito bem. Será que é limitada?");
		mes .@n$;
		mes "Hehe... Todo mundo adora. Mas houve alguns problemas antes.";
		next;
		select("Problemas? Existe algum peixe com defeito?");
		mes .@n$;
		mes "Não, o peixe não tem qualquer problema. O problema é os lotes de óleo de peixe produzidos após o processamento. Isso é um desperdício, para um processo industrial.";
		next;
		mes .@n$;
		mes "No entanto, depois de um processo de refino e revitalização, este óleo de peixe tornou-se valioso para usar no cuidado de equipamentos antigos e lubrificante industrial.";
		next;
		mes .@n$;
		mes "Além disso, este óleo é tão útil para relíquias separadas de armas equipadas que não podemos comprar Lubrificante Sofisticado e Lubrificante Comum com Zeny.";
		next;
		mes .@n$;
		mes "Bem, não se preocupe com dinheiro. Certamente o Lubrificante Sofisticado é caro. Se você pagar algum zeny, posso dar-lhe o lubrificante mais barato.";
		next;
		mes .@n$;
		mes "Eu não tenho certeza sobre a qualidade do sucesso. De qualquer forma, ele é muito barato, certo?";
		close;
	default:
		set .@equip_num, .@equip_slot[.@i-3];
		if (!getequipisequiped(.@equip_num)) {
			mes .@n$;
			if (.@Jeremy)
				mes "Nesta parte, não há nada?";
			else
				mes "Não há nada sobre essa parte?";
			close;
		}
		break;
	}

	setarray .@equip_card[0], getequipcardid(.@equip_num,0),getequipcardid(.@equip_num,1),getequipcardid(.@equip_num,2),getequipcardid(.@equip_num,3);
	set .@mvp_list$,
		"|4408|4128|4456|4168|4142"+  //Gloom_Under_Night_Card, Golden_Bug_Card, Nidhogg_Shadow_Card, Dark_Lord_Card, Doppelganger_Card
		"|4134|4137|4386|4407|4357"+  //Dracula_Card, Drake_Card, Detale_Card, Randgris_Card, B_Seyren_Card
		"|4146|4132|4147|4372|4145"+  //Maya_Card, Mistress_Card, Baphomet_Card, Bacsojin_Card, Berzebub_Card
		"|4374|4352|4367|4236|4425"+  //Apocalips_H_Card, B_Ygnizem_Card, B_Shecil_Card, Amon_Ra_Card, Atroce_Card
		"|4359|4123|4144|4135|4143"+  //B_Eremes_Card, Eddga_Card, Osiris_Card, Orc_Load_Card, Orc_Hero_Card
		"|4263|4131|4430|4276|4419"+  //Incant_Samurai_Card, Moonlight_Flower_Card, Ifrit_Card, Lord_Of_Death_Card, Ktullanux_Card
		"|4403|4399|4376|4441|4302"+  //Kiel_Card, Thanatos_Card, Lady_Tanee_Card, Fallen_Bishop_Card, Tao_Gunka_Card
		"|4305|4148|4318|4121|4365"+  //Turtle_General_Card, Pharaoh_Card, Knight_Windstorm_Card, Phreeoni_Card, B_Katrinn_Card
		"|4363|4324|4361|4330|4342|"; //B_Magaleta_Card, Garm_Card, B_Harword_Card, Dark_Snake_Lord_Card, Rsx_0806_Card

	if (.@Jeremy) {
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Sistema de encantamento de armadura
				set .@equip_card[.@i],0;
		}
		if (!getarraysize(.@equip_card)) {
			mes .@n$;
			mes "Não há carta equipada. Você quer verificar de novo?";
			close;
		}
		if ((.@equip_card[0] && compare(.@mvp_list$,"|"+.@equip_card[0]+"|")) ||
			(.@equip_card[1] && compare(.@mvp_list$,"|"+.@equip_card[1]+"|")) ||
			(.@equip_card[2] && compare(.@mvp_list$,"|"+.@equip_card[2]+"|")) ||
			(.@equip_card[3] && compare(.@mvp_list$,"|"+.@equip_card[3]+"|")))
			set .@boss_chk,1;
	} else {
		// O Oficial script do "Richard" usa uma lista codificada, incluindo todos os itens possíveis.
		//if (!getequipisequiped(.@equip_num)) {
		//	mes "[Richard]";
		//	mes "Sinto muito. Nós não fornecemos esse serviço ainda.";
		//	close;
		//}

		mes "[Richard]";
		mes "Qual o número do espaço que você quer separar o carta? A partir do espaço da esquerda, eles são classificados 1,2,3,4.";
		next;
		set .@menu$,"";
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] && .@equip_card[.@i] < 4700) // Sistema de encantamento de armadura
				set .@menu$, .@menu$+"Espaço "+(.@i+1)+" - "+getitemname(.@equip_card[.@i])+":";
			else
				set .@menu$, .@menu$+"^777777Espaço "+(.@i+1)+" - Sem carta^000000:";
		}
		set .@i, select("Pare:"+.@menu$);
		switch(.@i) {
		case 1:
			mes .@n$;
			mes "Sempre que você precisa dos meus serviços, por favor, venha até mim.";
			close;
		default:
			set .@slot, .@i-2;
			if (.@equip_card[.@slot] == 0 || .@equip_card[.@slot] >= 4700) {
				mes .@n$;
				mes "Este espaço não está equipado com nenhuma carta. Por que você não verifica de novo?";
				close;
			}
			break;
		}
		if (compare(.@mvp_list$,"|"+.@equip_card[.@slot]+"|"))
			set .@boss_chk,1;
	}
	if (.@boss_chk == 0) {
		mes .@n$;
		if (.@Jeremy)
			mes "Exceto cartas, ^ff0000todos os efeitos encantados desaparecerão.^000000 Se você concorda com isso, por favor escolha o tipo de serviço:";
		else
			mes "Por favor, escolha a taxa de serviço.";
		next;
		set .@menu$,
			"Next time...:"+
			((Zeny >= 1000000)?"Usar 1,000,000z (Não use item especial):":"^999999Usar 1,000,000z (Insuficiente)^000000:")+
			((countitem(6441))?"Usar Lubrificante Sofisticado:":"^999999Lubrificante Sofisticado (Insuficiente)^000000:")+
			((countitem(6440))?"Usar Lubrificante Comum":"^999999Lubrificante Comum (Insuficiente)^000000");
		switch(select(.@menu$)) {
		case 1:
			mes .@n$;
			mes "Sempre que você precisa dos meus serviços, visite-me aqui.";
			close;
		case 2:
			if (Zeny < 1000000) {
				mes .@n$;
				mes "Você não tem zeny. Por favor, volte quando puder pagar a taxa.";
				close;
			}
			mes .@n$;
			mes "Este é um equipamento muito antigo. Existe uma taxa elevada de chance em destruir as cartas ou equipamento durante o trabalho. Tem certeza de que deseja continuar?";
			next;
			if(select("Outra hora...:Continue") == 1) {
				mes .@n$;
				mes "Sempre que você precisa dos meus serviços, visite-me aqui.";
				close;
			}
			set .@sf_c_num,150;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			set Zeny, Zeny - 1000000;
			break;
		case 3:
			if (countitem(6441) == 0) {
				mes .@n$;
				mes "Você não pussui o Lubrificante Sofisticado.";
				close;
			}
			mes .@n$;
			mes "Se você usar o Lubrificante Sofisticado, a taxa de destruição será diminuída muito, mas não posso dar-lhe uma garantia de 100%. Tem certeza de que deseja continuar?";
			next;
			if(select("Outra hora...:Continue") == 1) {
				mes .@n$;
				mes "Sempre que você precisa dos meus serviços, visite-me aqui.";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,75;
			set .@sf_w_num,75;
			delitem 6441,1; //High_RankLubricant
			break;
		case 4:
			if (countitem(6440) == 0) {
				mes .@n$;
				mes "Você não possui o Lubrificante Comum.";
				close;
			}
			mes .@n$;
			mes "Se você usar o Lubrificante Comum, a taxa de destruição será diminuída muito, mas não posso dar-lhe uma garantia de 100%. Tem certeza de que deseja continuar?";
			next;
			if(select("Outra hora...:Continue") == 1) {
				mes .@n$;
				mes "Sempre que você precisa dos meus serviços, visite-me aqui.";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			delitem 6440,1; //General_Lubricant
			break;
		}
	} else if (.@boss_chk == 1) {
		mes .@n$;
		mes "Este equipamento contém uma carta de MVP preciosa. Esta carta não pode ser separada com lubrificante. Se você me trouxer o super  ^^000000 0000ffÓleo Removedor Especial, vou ser capaz de trabalhar.";
		next;
		switch(select("Outra hora...:Eu tenho um Óleo Removedor Especial.")) {
		case 1:
			mes .@n$;
			mes "Sempre que você precisa dos meus serviços, visite-me aqui.";
			close;
		case 2:
			if (countitem(6443) == 0) {
				mes .@n$;
				mes "Você não tem um Óleo Removedor Especial.";
				close;
			}
			break;
		}
		mes .@n$;
		if (.@Jeremy) {
			mes "Exceto cartas, ^ff0000todos os efeitos encantados desaparecerão.^000000 Se você concorda com isso, por favor escolha o tipo de serviço:";
			set .@menu$,"Certo, vamos lá!";
		} else {
			mes "Posso continuar?";
			set .@menu$,"Claro. Basta fazer rapidamente!";
		}
		next;
		switch(select("Outra hora...:"+.@menu$)) {
		case 1:
			mes .@n$;
			mes "Sempre que você precisa dos meus serviços, visite-me aqui.";
			close;
		case 2:
			set .@sf_c_num,60;
			set .@sf_r_num,60;
			set .@sf_w_num,60;
			delitem 6443,1; //Sillit_Pong_Bottle
			break;
		}
	}

	set .@equip_id, getequipid(.@equip_num);
	set .@equip_refine, getequiprefinerycnt(.@equip_num);
	delequip .@equip_num;

	// Chance of retaining refine level.
	if (rand(1,.@sf_r_num) >= 61)
		set .@equip_refine,0;

	if (.@Jeremy) {
		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,0,0;
		}

		// Chance of retaining cards.
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i]) {
				if (rand(1,.@sf_c_num) < 61)
					getitem .@equip_card[.@i],1;
				else
					set .@card_break,1;
			}
		}
	} else {
		set .@card, .@equip_card[.@slot];
		set .@equip_card[.@slot],0;

		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		}

		// Chance of retaining card.
		if (rand(1,.@sf_c_num) < 61)
			getitem .@card,1;
		else
			set .@card_break,1;
	}

	// Display corresponding effect.
	if (!.@equip_safe && .@card_break)
		specialeffect2 EF_LORD;
	else if (.@equip_safe && .@card_break)
		specialeffect2 EF_SUI_EXPLOSION;
	else if (!.@equip_safe && !.@card_break)
		specialeffect2 EF_FIREPILLAR;
	else
		specialeffect2 EF_MAXPOWER;

	// Output results.
	mes "-- Resultado da Separação de Cartas --";
	if (.@equip_safe) {
		mes "Não ocorreu falhas durante o processo de separação de cartas.";
		mes "^0000FFEquipamentos separados normalmente.^000000";
	} else {
		mes "ocorreu falhas durante o processo de separação de cartas.";
		mes "O Equipamento foi danificado. ^ff0000Irrecuperável.^000000";
	}
	mes "-------------------";
	if (!.@card_break) {
		mes "Nenhum problema no espaço de cartas do equipamento ocorreu durante o processo de separação de cartas.";
		mes "^0000ffCartas separadas com sucesso.^000000";
	} else {
		mes "Ocorreram problemas no espaço de cartas do equipamento durante o processo de separação de cartas.";
		mes "As Cartas foram danificadas. ^ff0000Irrecuperável.^000000";
	}
	next;
	mes .@n$;
	mes "Este foram os resultados da separação de cartas. Por favor, volte sempre.";
	close;
}
malangdo,215,166,4	duplicate(CardSeparation_mal)	Jeremy#pa0829	4_CAT_DOWN
malangdo,208,166,6	duplicate(CardSeparation_mal)	Richard#pa0829	4_CAT_ADV1