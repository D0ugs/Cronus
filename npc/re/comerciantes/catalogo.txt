//===== Cronus Script ===========================================
//= Catalogador Mágico
//===== Por: ====================================================
//= skyiing, Scriptor
//===== Versão Atual: ===========================================
//= 1.2
//===== Descrição: ==============================================
// = [Conversão Oficial] 
// = Catálogos que permite os jogadores facilmente pesquisar por itens no jogo, 
// = E comprar / vender diretamente da interface do catálogo.
//===== Comentários Adicionais: =================================
//= 1.0 Moved from root directory. [Euphy]
//= 1.1 Updated dialogue. [Euphy]
//= 1.2 Tradução [Evanio]
//===============================================================

moc_para01,22,16,5	script	Catalogador Mágico#ca01	4_M_BIBI,{
	if (!checkweight(1301,3)) {
		mes "- Você tem muitos itens, por favor, esvazie seu inventário. -";
		close;
	}
	set .@ticket_cost, 200;
	mes "[Catalogador Mágico]";
	mes "Olha só... a Guilda dos Magos";
	mes "em Geffen está agora";
	mes "vendendo o novo item";
	mes "^007777Catálogo de Vendas Prata^000000!";
	next;
	mes "Ele está olhando para as palavras";
	mes "escritas em sua mão.";
	mes "Parece estar muito ocupado";
	mes "para olhar para você.";
	next;
	mes "[Catalogador Mágico]";
	mes "Um novo catálogo";
	mes "acaba de sair!";
	mes "Você pode verificar";
	mes "os itens que você pode negociar";
	mes "e os itens que estão à venda pelas";
	mes "lojas montadas na cidade que você estiver.";
	next;
	mes "[Catalogador Mágico]";
	mes "O ^007777Catálogo de Venda Prata^000000";
	mes "custa apenas 200z!";
	mes "Você pode comprar apenas 50 peças de cada vez!";
	next;
	switch(select("Comprar o ^007777Catálogo de Venda Prata^000000:Eu não preciso disso!:Você não se parece com um vendedor!")) {
	case 1:
		mes "[Catalogador Mágico]";
		mes "Você... precisa de todas as 50 peças?";
		mes "Ou precisa de menos?";
		next;
		while (1) {
			input .@input;
			mes "[Catalogador Mágico]";
			if (.@input == 0) {
				mes "A negociação foi cancelada!";
				mes "Eu não sei o que está pretendendo...";
				mes "O que devo fazer?";
				emotion e_swt2,1;
				close;
			} else if (.@input < 1 || .@input > 50) {
				mes "Você pode comprar até 50 peças.";
				next;
			} else
				break;
		}
		set .@sell, .@ticket_cost * .@input;
		mes "O número total de catálogo(s) que você está tentando comprar é " + .@input + ".";
		mes "Irá custar " + .@sell + " zeny.";
		if (Zeny < .@sell) {
			mes "Aparentemente você não tem dinheiro suficiente.";
			mes "Você pode vender o seu equipamento";
			mes "para ganhar mais dinheiro... se quiser, é claro.";
			close;
		}
		mes "Ok, aqui vou eu.";
		set Zeny, Zeny - .@sell;
		getitem 12580,.@input; //Vending_Search_Scroll
		close;
	case 2:
		mes "[Catalogador Mágico]";
		mes "Argh... você realmente precisa desse";
		mes "item... Você precisa que eu";
		mes "explique de novo? Eu não sei";
		mes "o que fazer agora...";
		mes "O que devo fazer?";
		emotion e_swt2,1;
		close;
	case 3:
		mes "[Catalogador Mágico]";
		mes "Minha real classe é Mago.";
		mes "Eu fui aprender algumas habilidades";
		mes "de comércio na Guilda dos Mercadores para";
		mes "poder vender esse novo item, mas eles estavam";
		mes "me colocando para vender vassouras. Então";
		mes "eu decidi vender por mim mesmo.";
		next;
		mes "[Catalogador Mágico]";
		mes "Há muitos aventureiros";
		mes "aqui que vem e vão.";
		mes "Eu tenho certeza que conseguirei vender todos eles bem rápido.";
		mes "Você não acha?";
		mes "Então... vamos lá!! Compre, por favor! POR FAVOR...!";
		close;
	}
}